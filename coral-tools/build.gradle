apply plugin: 'li-java'
apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
  compile project(':coral-presto')
  compile project(':coral-pig')
  compile project(':coral-spark')
  compile(project(':coral-tests')) {
    // These exclusions are required to prevent duplicate classes with coral-calcite
    exclude group: 'org.apache.calcite', module: 'calcite-core'
    exclude group: 'org.apache.calcite', module: 'calcite-avatica'
  }
  compile deps.'presto-parser'
  compile 'com.linkedin.spark:spark-sql_2.11:2.3.0.+'
}

// Create a fat jar so that we can run the translate/translateAll tasks on the gateway machine without relying
// on gradle to pull in dependencies.
shadowJar {
  classifier = 'all'
  zip64 true
}

artifacts {
  archives jar, javadocJar, sourcesJar, shadowJar
}

task translate(type: JavaExec) {
  main = "com.linkedin.coral.tools.SingleViewTranslation"
  classpath = sourceSets.main.runtimeClasspath
  if (project.hasProperty('view')) {
    systemProperty "view", "$view"
  }

  if (project.hasProperty('language')) {
    systemProperty "language", "$language"
  }
}

task translateAll(type: JavaExec) {
  main = "com.linkedin.coral.tools.AllViewTranslation"
  classpath = sourceSets.main.runtimeClasspath

  if (project.hasProperty('resultDir')) {
    systemProperty "resultDir", "$resultDir"
  }

  if (project.hasProperty('exclude')) {
    systemProperty "exclude", "$exclude"
  }

  if (project.hasProperty('include')) {
    systemProperty "include", "$include"
  }

  if (project.hasProperty('language')) {
    systemProperty "language", "$language"
  }
}
