apply plugin: 'li-java'
apply plugin: 'maven-publish'
apply plugin: 'shade'


dependencies {
  compile deps.'javax-annotation'
  // NOTE: jline is needed as testCompile for PigUnit tests because it is missing from Pig runtime
  testCompile deps.'jline'

  compile(project(path: ':coral-hive'))

  testCompile(deps.'pig'.'pig') { transitive = false }
  testCompile(deps.'pig'.'pigunit') { transitive = false }

  // Pull in hive-metastore transitively through hive-exec.
  // Use shaded hive-exec/hive-metastore dependencies for Coral to prevent classpath
  // conflicts with DaliStorage/PiggyBank.
  compile(deps.'hive'.'hive-exec-core') {
    exclude group: 'org.apache.avro', module: 'avro-tools'
    // These exclusions are required to prevent duplicate classes since we include
    // shaded coral-calcite jar above
    exclude group: 'org.apache.calcite', module: 'calcite-core'
    exclude group: 'org.apache.calcite', module: 'calcite-avatica'
    exclude group: 'jline', module: 'jline'
  }

  testCompile deps.'hadoop'.'hadoop-mapreduce-client-common'
  testCompile deps.'kryo'
}

def pomPath = "$buildDir/${name}.pom"
model {
  tasks.generatePomFileForMavenJavaPublication {
    destination = file("$pomPath")
  }

  tasks.jar {
    dependsOn tasks.generatePomFileForMavenJavaPublication
  }
}

// Create a fat jar for the Pig engine to pull in as a dependency.
shadeJar {
  doNotShade "com/linkedin/coral/**"
  // force the JDBC classloader to use the shaded version of the JDBC Calcite driver
  relocate "jdbc:calcite:", "jdbc:coralpig:"
  // include hive-metastore and hive-exec dependencies in the jar
  includeRestrictedDependencies "com.linkedin.hive:hive-metastore"
  includeRestrictedDependencies "com.linkedin.hive:hive-exec"
  // shade with prefix "coral_pig" for coral integration purposes.
  shadePrefix "coral_pig"
  mergeServiceFiles()
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
}

// need to explicitly set this because we are generating pom files
generateModuleArtifactSpec.defaultArtifact = jar
artifacts {
  archives file: file("$pomPath"), name: "$name", type: "pom"
  archives jar, javadocJar, sourcesJar
}