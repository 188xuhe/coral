apply plugin: 'java'
apply plugin: 'maven-publish'

dependencies {
  compile('com.linkedin.calcite:calcite-core:1.21.1.0') {
    artifact {
      name = 'calcite-core'
      extension = 'jar'
      type = 'jar'
      classifier = 'shaded'
    }
  }

  compile(deps.'hive'.'hive-metastore') {
    exclude group: 'com.linkedin.metastore-autometrics', module: 'autometrics-reporter'
    exclude group: 'com.linkedin.metastore-audit', module: 'metastore-audit-logging'
    // avro-tools brings in whole bunch of hadoop classes causing duplicates and conflicts
    exclude group: 'org.apache.avro', module: 'avro-tools'
  }
  compile deps.'hadoop'.'hadoop-common'

  // required for hiveSQL parser code
  compile deps.'antlr-runtime'

  testCompile(deps.'hive'.'hive-exec-core') {
    exclude group: 'org.apache.avro', module: 'avro-tools'
    // These exclusions are required to prevent duplicate classes since we include
    // shaded jar above
    exclude group: 'org.apache.calcite', module: 'calcite-core'
    exclude group: 'org.apache.calcite', module: 'calcite-avatica'
  }

  testCompile deps.'hadoop'.'hadoop-mapreduce-client-core'
  testCompile deps.'kryo'
}

def pomPath = "$buildDir/${name}.pom"

model {
  tasks.generatePomFileForMavenJavaPublication {
    destination = file("$pomPath")
  }

  tasks.jar {
    dependsOn tasks.generatePomFileForMavenJavaPublication
  }
}


publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId = 'com.linkedin.coral'
      from components.java
    }
  }
}

task sourcesJar(type: Jar) {
  from sourceSets.main.output
  archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
  from javadoc
  archiveClassifier = 'javadoc'
}

artifacts {
  archives file: file(pomPath), name: "$name", type: "pom"
  archives jar, javadocJar, sourcesJar
}