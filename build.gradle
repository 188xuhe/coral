configurations {
  provided
}

buildscript {
  repositories {
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0'
    classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
  }
}

allprojects {
  repositories {
    mavenCentral()
    // TODO: remove this after Calcite jar published to maven
    ivy {
      url 'http://artifactory.corp.linkedin.com:8081/artifactory/release'
      layout 'pattern', {
        ivy '[organisation]/[module]/[revision]/[module]-[revision].ivy'
        artifact '[organisation]/[module]/[revision]/[artifact]-[revision](-[classifier]).[ext]'
        m2compatible = true
      }
    }
  }
}

subprojects {
  apply plugin: "com.github.hierynomus.license"

  license {
    header = file("${rootDir}/gradle/license/LICENSE_HEADER")
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    skipExistingHeaders true
    ignoreFailures false
    strictCheck true

    exclude '**/*.avsc'
    exclude '**/*.json'
  }

  plugins.withType(JavaPlugin) {
    dependencies {
      testCompile deps.'testing'
    }

    test {
      useTestNG()
    }
  }

  apply from: "${rootDir}/gradle/dependencies.gradle"
}
